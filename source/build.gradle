group 'com.github.vilmosnagy'
version '0.1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.5-2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'

idea.module.sourceDirs += file("$buildDir/generated/source/kapt/main")

kapt {
    generateStubs = true
}

repositories {
    mavenCentral()
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'CodeClimate KLint executable',
                'Implementation-Version': version,
                'Main-Class': 'com.github.vilmosnagy.codeclimate.klint.MainKt'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.github.shyiko:ktlint:0.2.2'
    compile 'com.github.salomonbrys.kotson:kotson:2.3.0'
    compile 'com.google.dagger:dagger:2.7'

    kapt 'com.google.dagger:dagger:2.7'
    kapt 'com.google.dagger:dagger-compiler:2.7'

    testCompile 'io.kotlintest:kotlintest:1.3.5'
    testCompile "com.nhaarman:mockito-kotlin:0.11.0"
    testCompile 'de.jodamob.kotlin:kotlin-runner-junit4:0.3.1'
    testCompile 'com.google.jimfs:jimfs:1.1'
}
